plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.2.0'
}

group = 'com.tsystemsmms'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

compileJava {
    options.deprecation = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.javaoperatorsdk:operator-framework-spring-boot-starter:2.0.1'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    // required for the fabric8 k8s client to grok k3d certificates
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.apache.bval:bval-jsr:2.0.5'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    // developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'io.fabric8:crd-generator-apt:5.12.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.javaoperatorsdk:operator-framework-spring-boot-starter-test:2.0.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

task copyCrd(type: Copy) {
    from('build/classes/java/main/META-INF/fabric8/coremediacontentclouds.cmcc.tsystemsmms.com-v1.yml') {
        rename '.*', 'cmcc-crd.yaml'
    }
    into 'k8s'
}

build.finalizedBy(copyCrd)

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

jib {
    from {
        image = "docker.io/library/openjdk:11-jre-slim"
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {}
    container {
        labels = [
                "org.opencontainers.image.description"  : "A Kubernetes operator that installs and configures a CoreMedia Content Cloud installation in a cluster.",
                "org.opencontainers.image.documentation": "https://github.com/T-Systems-MMS/cmcc-operator",
                "org.opencontainers.image.licenses"     : "Apache-2.0",
                "org.opencontainers.image.source"       : "https://github.com/T-Systems-MMS/cmcc-operator",
                "org.opencontainers.image.title"        : "CoreMedia Content Cloud Kubernetes Operator",
                "org.opencontainers.image.url"          : "https://github.com/T-Systems-MMS/cmcc-operator",
                "org.opencontainers.image.vendor"       : "T-Systems Multimedia Solutions GmbH"
        ]
    }
}
